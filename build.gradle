plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.2.10'
    id 'com.gradleup.shadow' version '9.1.0'
}

group = 'ltotj.minecraft'
version = '1.0-SNAPSHOT'

def kotlinVersion = '2.0.20'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    // For ProtocolLib
    maven {
        name = 'dmulloy2-repo'
        url "https://repo.dmulloy2.net/repository/public/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    compileOnly ("com.github.MilkBowl:VaultAPI:1.7") {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    // For Totem Packet
    compileOnly 'net.dmulloy2:ProtocolLib:5.4.0'

}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}

shadowJar {
    archiveClassifier.set('')
}

build.dependsOn shadowJar

test {
    useJUnitPlatform()
}

// KotlinとJavaのツールチェーンをJDK 21に統一
kotlin {
    jvmToolchain(21)
}

// Kotlinファイルを src/main/java 配下も対象にする
sourceSets {
    main {
        kotlin.srcDirs += 'src/main/java'
    }
    test {
        kotlin.srcDirs += 'src/test/kotlin'
    }
}